# vim:et sts=2 sw=2 ft=zsh
#
# s1ck94 theme
#
# Implemented after the now extinct version of minimal from S1cK94.
#
# Requires the `git-info` zmodule to be included in the .zimrc file.

prompt_s1ck94_help () {
  cat <<EOH
This prompt can be customized with:

    prompt s1ck94 [prompt_char] [on_color] [err_color]

The respective default values for each parameter are ❯, green, and red.
EOH
}

prompt_s1ck94_vimode() {
  case ${KEYMAP} in
    main|viins) print -n ${1} ;;
    *) print -n ${2} ;;
  esac
}

prompt_s1ck94_pwd() {
  local separator='%F{244}/%f'
  local current_dir="${PWD/#${HOME}/~}"
  if [[ ${current_dir} != '~' ]]; then
    # Can't use ${separator} with curly braces below. See Parameter Expansion Flags in zshexpn(1)
    current_dir="${${(@pj:$separator:M)${(@s:/:)current_dir:h}#?}%/}${separator}${current_dir:t}"
  fi
  print -n ${current_dir}
}

function zle-line-init zle-keymap-select {
  zle reset-prompt
  zle -R
}

prompt_s1ck94_precmd() {
  (( ${+functions[git-info]} )) && git-info
}

prompt_s1ck94_setup() {
  local prompt_char="${1:-❯}"
  local on_color="%F{${2:-green}}"
  local off_color='%f'
  local err_color="%F{${3:-red}}"
  local user_prompt="%(!.${on_color}.${off_color})${prompt_char}"
  local jobs_prompt="%(1j.${on_color}.${off_color})${prompt_char}"
  local vimode_prompt="\$(prompt_s1ck94_vimode ${on_color} ${off_color})${prompt_char}"
  local status_prompt="%(0?.${on_color}.${err_color})${prompt_char}"

  zle -N zle-line-init
  zle -N zle-keymap-select

  autoload -Uz add-zsh-hook && add-zsh-hook precmd prompt_s1ck94_precmd

  prompt_opts=(cr percent sp subst)

  zstyle ':zim:git-info:branch' format '%b'
  zstyle ':zim:git-info:commit' format '%c'
  zstyle ':zim:git-info:dirty' format ${err_color}
  zstyle ':zim:git-info:diverged' format ${err_color}
  zstyle ':zim:git-info:behind' format '%F{yellow}'
  zstyle ':zim:git-info:ahead' format ${off_color}
  zstyle ':zim:git-info:keys' format \
    'prompt' " \$(coalesce %D %V %B %A ${on_color})%b%c"

  PS1="${user_prompt}${jobs_prompt}${vimode_prompt}${status_prompt}%f "
  RPS1='$(prompt_s1ck94_pwd)${(e)git_info[prompt]}'
}

prompt_s1ck94_preview () {
  if (( # )); then
    prompt_preview_theme s1ck94 "${@}"
  else
    prompt_preview_theme s1ck94
    print
    prompt_preview_theme s1ck94 ']'
  fi
}

prompt_s1ck94_setup "${@}"
